
class EngineMainMenu from SinusMenu

    function Setup()
        self.Add( OpenEnginePackageMenu, "Load Game" )
        self.Add( [OpenMenu, EngineSettingsMenu], "Settings" )
        self.Add( Engine.Reset, "[Debug: Hard Reset!]" )
        self.Add( [OpenEngineFileBrowserMenu, ""], "[Debug: VFS Files...]" )
        self.Add( self.OnGoBack, "Exit" )
    end
    
    function OnGoBack()
        EngineConfig.Save()
        Engine.Exit()
    end
    
    function OnUpdate()
        sx, sy = Screen.GetSize()
        self.x = int((sx - self.totalW) / 2)
        self.y = int((sy - self.totalH) / 2)
    end

end

class EngineSettingsMenu from SinusMenu

    _music = []
    _musicPos = 0
    _cameraFocusTypes = ["InstantFocus", "EightWayFocus", "SmoothFocus"]
    _cameraPos = 0
    
    function Setup()
        self.Add( { => }, "Music volume" )
        self.Add( { => }, "Sound volume" )
        self.Add( [self.UpdateMusic, 1], "Menu music" )
        self.Add( OpenJoystickTest, "Joystick Test" )
        self.Add( ToggleDebugOverlay, "Toggle debug overlay" )
        self.Add( [self.CycleCameraFocus, 1], "Camera focus type" )
        self.Add( self.OnGoBack, "Back" )
        self.LoadMusic()
        
        self._cameraPos = self._cameraFocusTypes.find(EngineConfig.CameraFocusType)
    end
    
    function LoadMusic()
        self._music = filter( IsSupportedAudioFile, VFS.GetFileList("music") )
        if len(self._music)
            self._musicPos = self._music.find(EngineConfig.menuMusic)
            if self._musicPos < 0
                self._musicPos = 0
                forceStart = true
            else
                forceStart = false
            end
            self.UpdateMusic(0, forceStart)
        end
    end
    
    function Update()
        self.SinusMenu.Update()
        self.Text(0, "Music Volume [" + EngineConfig.musicVolume + "]")
        self.Text(1, "Sound Volume [" + EngineConfig.soundVolume + "]")
        self.Text(2, "Menu Music [" + EngineConfig.menuMusic + "]")
        self.Text(5, "Camera focus type [" + EngineConfig["CameraFocusType"] + "]")
    end
    
    function UpdateMusic(dir, forceStart)
        count = len(self._music)
        if not count: return
        oldm = self._music[self._musicPos]
        self._musicPos = climit(self._musicPos + dir, 0, count - 1)
        m = self._music[self._musicPos]
        
        EngineConfig.menuMusic = m
        if (m != oldm) or forceStart
            Music.Play(m)
        end
    end
    
    function CycleCameraFocus(dir)
        count = len(self._cameraFocusTypes)
        if not count: return
        self._cameraPos = climit(self._cameraPos + dir, 0, count - 1)
        c = self._cameraFocusTypes[self._cameraPos]
        
        EngineConfig["CameraFocusType"] = c
        Camera.SetType(c)
    end
    
    function OnKeyLeft(): self.HandleLR(true)
    function OnKeyRight(): self.HandleLR(false)
    
    function HandleLR(L)
        done = true
        switch self.sel
            case 0
                EngineConfig.musicVolume = climit(EngineConfig.musicVolume + (L ? -5 : 5), 0, MAX_VOLUME)
                Music.SetVolume(EngineConfig.musicVolume)
                
            case 1
                EngineConfig.soundVolume = climit(EngineConfig.soundVolume + (L ? -5 : 5), 0, MAX_VOLUME)
                
            case 2
                self.UpdateMusic(L ? -1 : 1)
                
            case 5
                self.CycleCameraFocus(L ? -1 : 1)
                
            default
                done = false
        end
        if done
            self.Update()
            self.sndSelect.Play()
        end
    end
    
    function OnGoBack()
        EngineConfig.Save()
        OpenMenu(EngineMainMenu)
    end

    function OnUpdate()
        sx, sy = Screen.GetSize()
        self.x = int((sx - self.totalW) / 2)
        self.y = int((sy - self.totalH) / 2)
    end
end

class EngineFileListMenu from SinusMenu
    
    init
        self.rowSpacing = 3
    end
    
    function OnGoBack()
        OpenMenu(EngineMainMenu)
    end
    
    function OnUpdate()
        sx, sy = Screen.GetSize()
        self.x = int((sx - self.totalW) / 2)
        self.y = int((sy - self.totalH) / 2)
    end

end


function OpenEngineMainMenu()
    m = OpenMenu(EngineMainMenu)
    Music.Play(EngineConfig.menuMusic)
    Screen.SetCamera(0, 0)
    Engine.SetSpeed(1.0)
    Engine.ResourceCleanup() // we don't want any remains from previously loaded packages
end

function OpenEnginePackageMenu()
    VFS.Reload()
    packs = PackageSystem.Enumerate()
    packs = PackageSystem.FilterByType(packs, ["game", "demo", "test"])
    
    m = EngineFileListMenu()
    
    for name, entries in packs
        s = entries.get("name")
        if not s: s = "[Package: " + name + "]" // more fail safe
        m.Add( [LoadPackageFile, name], s )
    end
    m.Add(m.OnGoBack, "--- Back ---")
    
    OpenMenu( { => m } ) // use lambda to fake a constructor, because we have already initialized the menu

end

function OpenEngineFileBrowserMenu(path) // for debugging

    > "Entering path: '", path, "'"
    m = EngineFileListMenu()
    m.rowSpacing = 0
    
    d = VFS.GetDirList(path)
    makeFN = { p, x => len(p) ? (p + "/" + x) : x }
    if d
        for s in d
            m.Add( [OpenEngineFileBrowserMenu, makeFN(path, s)], "[" + s + "]" )
        end
    end
    
    f = VFS.GetFileList(path)
    if f
        for s in f
            m.Add( { => > ByteBuf(VFS.GetFileAsBuf(makeFN(path, s)), true).readString() }, s ) // output first readable string to console, for testing
        end
    end
    
    m.Add(m.OnGoBack, "--- Back ---")
    
    OpenMenu( { => m } ) // use lambda to fake a constructor, because we have already initialized the menu
    
end

function OpenJoystickTest()
    CloseCurrentMenu()
    ShowJoystickTestScreen( [OpenMenu, EngineSettingsMenu] )
end

function ToggleDebugOverlay()
    EngineConfig["enableDebugHooks"] = not EngineConfig.get("enableDebugHooks")
    CallScheduler.Schedule( dbg_ApplyHooks, 1 )
end

function LoadPackageFile(s)
    > "Selected Package: ", s
    loadfunc = function()
        try
            return PackageSystem.LoadRoot(s) // there may be an explicitly raised error somewhere in the package dependencies upon loading
        end
    end
    CallScheduler.Schedule( loadfunc, 1 )
end


CallScheduler.Schedule( OpenEngineMainMenu, 1 )


export OpenEnginePackageMenu
