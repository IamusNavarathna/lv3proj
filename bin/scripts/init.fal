> "*** Falcon Init start ***"

script_dirs = .[
    // note that the paths are always relative to the exe position
    "scripts/util"
    "scripts/game"
    "scripts/sprites"
]



// Load all scripts from 'path', resolving dependencies between scripts
// If there are multiple scripts depending on each other, it will repeatedly try to load what it can until there are no more errors,
// or it has loaded everything it could. 
// Note: This does not resolve circular dependencies! (A depends on B depends on A, for example),
//       and can not resolve conflicts between files that reside in different directories
function GetScriptsFromDir(path)
    try
        scriptdir = Directory(path)
    catch in err
        > "Can't load anything from '" + path + "', directory does not exist."
        return
    end
    files = List()
    ftype = "fal"
    while(fn = scriptdir.read())
        ext = strSplit(fn,".")[-1] // last array element is the extension
        if(strLower(ext) == ftype and len(fn) > 4)
            files.push( [fn, path] )
        end
    end
    scriptdir.close()
    if(not files.len())
        > "Nothing to do in '" + path + "', directory is empty"
    end
    return files
end

function LoadScriptsFromList(files)
    global _cmpl
    done_count = 0
    collect_errors = false
    errors = [ => ]
    loop
        startlen = files.len()
        // > "LoadScriptsFromList starting with " + startlen + " scripts"
        for fn, path in files
            sc = path + "/" + fn
            // > "Falcon:Init: Loading script '" + sc + "'"
            try
                include_ex(fn, "", path) // will raise an error if loading failed (be it unresolved symbols or something else)
                > "** Falcon script '" + sc + "' loaded"
                done_count++
                continue dropping // when we reached this point loading the file was sucessful, remove it from the list
            catch in err
                if(collect_errors)
                    errors[sc] = err
                else
                    //> "... failed, trying next one"
                end
            end
        end
        endlen = files.len()
        
        if(startlen == 0)
            if(done_count > 0)
                > "All " + done_count + " scripts loaded successfully"
            end
            break
        elif(startlen == endlen) // no file could be loaded in this run, there are dependencies we cant resolve or just files with syntax error
            if(collect_errors)
                // >  "Falcon: " + done_count + " out of " + (done_count + endlen) + " scripts loaded."
                > "Unable to load all scripts. Files:"
                for sc, err in errors
                    > "[ " + sc + " ]"
                end
                > ""
                > "----= Errors =----------------------------------------"
                for sc, err in errors
                    > err
                    > ""
                end
                break           
            else
                collect_errors = true
                // > "LoadScriptsFromList -- error collect in next loop"
            end
            
        end
        // > "LoadScriptsFromList loop finished with " + endlen + " scripts")
    end
end

allfiles = List()
for d in script_dirs
    files = GetScriptsFromDir(d)
    while(files.len()): allfiles.push(files.pop())
end

LoadScriptsFromList(allfiles)

> "*** Falcon Init done ***"


export GetScriptsFromDir, LoadScriptsFromList

