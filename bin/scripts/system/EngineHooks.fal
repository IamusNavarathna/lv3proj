
// InputEventHandler() input hooks
_rawhooks = []

// GameUpdate() hooks
_uphooks = []

// PostRender() hooks
_rhooks = []

// ObjectCreated() hooks
_ochooks = []

// MouseEventHandler() hooks
_mhooks = []

// --- called by core whenever input occurs ---
function InputEventHandler(e)
    global _rawhooks
    
    for func in _rawhooks
        try
            func(e)
        catch in err
            > "ERROR: InputEventHandler(): unhandled exception in hook '", func, "'"
            inspect(func)
            > err
        end
    end
end

function _EnsureCallable(by, func)
    if not isCallable(func) // helps to find errors
        inspect(func)
        raise by.toString() + "(): '" + func + "' not callable"
    end
end

function RegisterRawInputHook(func)
    global _rawhooks
    _EnsureCallable(fself, func)
    _rawhooks.add(func)
end

function UnregisterRawInputHook(func)
    global _rawhooks
    _EnsureCallable(fself, func) // for debug only
    _rawhooks.del(func)
end

function GetRawInputHookCount()
    global _rawhooks
    return len(_rawhooks)
end


// called by the core on every loop
function GameUpdate(ms)
    global _uphooks
    
    // process registered functions. if one returns true, remove from list
    for func in _uphooks
        try
            func(ms)
        catch in err
            > "ERROR: GameUpdate(): unhandled exception in hook '", func, "'"
            inspect(func)
            > err
            > "---> Dropping hook to prevent further console spam"
            continue dropping
        end
    end
end

function RegisterUpdateHook(func)
    global _uphooks
    _EnsureCallable(fself, func)
    _uphooks.add(func)
end

function UnregisterUpdateHook(func)
    global _uphooks
    _EnsureCallable(fself, func) // for debug only
    _uphooks.del(func)
end

function GetUpdateHookCount()
    global _uphooks
    return len(_uphooks)
end

// called by the core after every rendering
function PostRender()
    global _rhooks
    
    for func in _rhooks
        try
            func()
        catch in err
            > "ERROR: PostRender(): unhandled exception in a hook:"
            > func
            > err
            > "---> Dropping hook to prevent further console spam"
            continue dropping
        end
    end
end

function RegisterRenderHook(func)
    global _rhooks
    _EnsureCallable(fself, func)
    _rhooks.add(func)
end

function UnregisterRenderHook(func)
    global _rhooks
    _EnsureCallable(fself, func) // for debug only
    _rhooks.del(func)
end

function GetRenderHookCount()
    global _rhooks
    return len(_rhooks)
end

// called whenever an object was created
function ObjectCreated(obj)
    global _ochooks
    
    for func in _ochooks
        try
            func(obj)
        catch in err
            > "ERROR: ObjectCreated(): unhandled exception in a hook:"
            > func
            > err
            // no need to drop, here
        end
    end
end

function RegisterObjectCreateHook(func)
    global _ochooks
    _EnsureCallable(fself, func)
    _ochooks.add(func)
end

function UnregisterObjectCreateHook(func)
    global _ochooks
    _EnsureCallable(fself, func)
    _ochooks.del(func)
end

// called whenever an the mouse was moved or clicked
function MouseEventHandler(e)
    global _mhooks
    
    for func in _mhooks
        try
            func(e)
        catch in err
            > "ERROR: MouseEventHandler(): unhandled exception in a hook:"
            > func
            > err
            // no need to drop, here
        end
    end
end

function RegisterMouseInputHook(func)
    global _mhooks
    _EnsureCallable(fself, func)
    _mhooks.add(func)
end

function UnregisterMouseInputHook(func)
    global _mhooks
    _EnsureCallable(fself, func) // for debug only
    _mhooks.del(func)
end

export GameUpdate
export RegisterUpdateHook, UnregisterUpdateHook, GetUpdateHookCount

export InputEventHandler
export RegisterRawInputHook, UnregisterRawInputHook, GetRawInputHookCount

export MouseEventHandler
export RegisterMouseInputHook, UnregisterMouseInputHook

export PostRender
export RegisterRenderHook, UnregisterRenderHook, GetRenderHookCount

export ObjectCreated
export RegisterObjectCreateHook, UnregisterObjectCreateHook
