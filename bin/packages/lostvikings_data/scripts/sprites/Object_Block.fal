class Block from Object

    _sound = nil
    
    gfx = nil
    fallSpeed = 256
    pushSpeed = 38
    pushFriction = 380
    
    init
        self._sound = Sound("blockfall.ogg")
        tile = self.SetSprite(self.gfx)
        if not tile
            tile = self.SetSprite("misc/noimage32.png")
            if not tile: raise "File not found! (" + self.gfx + ") - not even placeholder (gfx/misc/noimage32.png)"
        end
        self.SetBlocking(true)
        self.SetBBox(0,0,32,32)
        self.SetCollisionEnabled(true)
        
        // init physics
        p = self.phys // note that p is a reference automatically, and all fields initialized with 0
        p.ymaxspeed = self.fallSpeed
        p.xmaxspeed = self.pushSpeed
        p.xfriction = self.pushFriction
        self.SetAffectedByPhysics(true)
    end
    
    function OnTouchedBy(side, who)
        //> "Block: OnTouchedBy(", side, ", ", who, ")"
        if not (side && SIDE_FLAG_SOLID): DbgBreak()
        
        if who.type >= OBJTYPE_PLAYER
            if side && SIDE_LEFT // pushing from left, move to right
                if self.CanMoveToDir(DIRECTION_RIGHT)
                    //self.x += 1
                    //who.x += 1
                    self.phys.xspeed = self.pushSpeed;
                    return true
                end
            elif side && SIDE_RIGHT // pushing from right, move to left
                if self.CanMoveToDir(DIRECTION_LEFT)
                    //self.x -= 1
                    //who.x -= 1
                    self.phys.xspeed = -self.pushSpeed;
                    return true
                end
            end
        end
        
        return true
    end
    
    function OnTouch(side, who)
        //> "Block: OnTouch(", side, ", ", who, ")"
        return false
    end
    
    function OnEnter(side, who)
        //> "Block: OnEnter(", side, ", ", who, ")"
        if side && (SIDE_TOP || SIDE_BOTTOM)
            select who
                case PlayerEx
                    //if not who.CanMoveToDir(side) // this is a bit buggy... "teleport bug" again
                        who.Kill(DEATH_CRUSH)
                    //end
            end
        end
    end
    
    function OnTouchWall(side, xs, ys)
        //> "Block::OnTouchWall(", side, ", ", xs, ", ", ys, ")"
        if side && (SIDE_BOTTOM || SIDE_TOP) and abs(ys) > 60.0
            if self._sound
                self._sound.Play()
            end
        end
    end

    
end

export Block
